@precedence { fieldRe @left, fieldDe @left, modelRe @left, modelDe @left, typeEnumIdent @left, typeSymbol @left, typeUnion @left, typeAtomic @left, arrayOf @left, type @left, trueFalseNull @left, objectKeySymbol @left }

@top root { entity (redent entity)* }

entity { Model | Enum }

modelBody { indent modelChild (!modelRe redent modelChild)* !modelDe dedent }
ModelName { identifier }
Model { ModelName modelBody?  }


modelChild { Field | describer }

FieldName { identifier }
Field { FieldName typeSpec? LabelString? exampleSpec? fieldBody?  }
fieldBody { indent fieldChild (!fieldRe redent fieldChild)* !fieldDe dedent }
fieldChild { describer }

EnumName { identifier }
EnumPipe { pipe }
Enum { EnumPipe EnumName typeSpec? enumBody? }
enumBody { indent enumChild (redent enumChild)* dedent }
enumChild { Variant | describer }

VariantName { identifier }
Variant { VariantName }

describer { singleDocumentationLine | multilineDocumentation | property | tags}
PropertyKey { identifier }
property { Period PropertyKey "=" Value }
Tag { identifier }
tags { OpenBracket Tag (comma Tag)* comma? CloseBracket }

exampleSpec { tilde Value }
typeSpec {  colon Type }
Type { typeInner  }
typeInner { !typeAtomic atomicType | !typeUnion UnionType | !arrayOf ArrayOfType }
ArrayOfType { OpenBracket !type typeInner CloseBracket }
nonUnionType { atomicType | ArrayOfType }
UnionType { nonUnionType (pipe nonUnionType)* }
atomicType { !typeSymbol TypeSymbol | !typeEnumIdent typeEnumShorthand }
TypeSymbol { identifier }
typeEnumShorthand {  TypeEnumVariant comma TypeEnumVariant (comma TypeEnumVariant)* }
TypeEnumVariant { identifier }



Value { complexValue }
AtomicValue { atomicValue }
atomicValue { ValueString | Bool | Null | Number | Symbol }
complexValue {  Array | Object | atomicValue }
Object { '{' objectKeyValue (',' objectKeyValue)* ','? '}' }
Array { '[' complexValue (',' complexValue)* ','? ']' }
objectKeyValue { (ObjectKeyString | ObjectKeySymbol) ':' complexValue }
ObjectKeyString { doubleQuoteString }
ObjectKeySymbol { symbol }
ValueString { doubleQuoteString }

Symbol { symbol }
Bool { true | false  }
TemplateParameter {"<" }

multilineDocumentation { DoubleCaret indent DocumentationString (redent DocumentationString)* dedent }
singleDocumentationLine { SingleDocumentationCaret DocumentationString }

@skip {} {
    LabelString { "'" (Escape | labelStringContent)* "'" }
    doubleQuoteString { "\"" (Escape | valueStringContent)* "\"" }
    TemplateString { "`" (Escape | templateStringContent)* "`" }
    DocumentationString { documentationString }
}

SingleDocumentationCaret { singleCaret }

@skip { whitespace }

@external tokens indentation from "./tokens.js" {
    indent
    redent
    dedent
}

@tokens {
    identifier { $[a-zA-Z] $[a-zA-Z0-9\-_]* }
    whitespace { $[ \n\r\t] }
    documentationString { ![^\n\r]+ }
    labelStringContent{ !['\\]+ }
    valueStringContent { !["\\]+ }
    templateStringContent { ![`\\]+ }
    colon { ":" }
    comma { "," }
    tilde { "~" }
    pipe { "|" }
    Period { "." }
    OpenBracket { "[" }
    CloseBracket { "]" }
    DoubleCaret { ">>" }
    singleCaret { ">" }
    hex { @digit | $[a-fA-F] }
    Escape { "\\" ($["\\\/bfnrt`<'] | "u" hex hex hex hex hex ) }
    true { "true" }
    false { "false" }
    Null { "null" }
    symbol { $[a-zA-Z] $[a-zA-Z0-9\-_]* }
    @precedence { true, false, Null, symbol }
    @precedence { indent, redent, dedent, documentationString, whitespace }
    int  { '0' | $[1-9] @digit* }
    frac { '.' @digit+ }
    Number { '-'? int frac? }
}


@context trackIndent from "./tokens.js"

@external propSource automnHighlighting from "./highlight.js"
